plugins {
    id 'com.android.library'
    id 'kotlin-android'
    id 'org.jetbrains.dokka-android'
}
apply from: '../sharedproperties.gradle'
android {
    compileSdk targetSDK

    defaultConfig {
        minSdkVersion minSDK
        targetSdkVersion targetSDK

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        consumerProguardFiles "consumer-rules.pro"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = '1.8'
    }

    dokka {
        includes = ["$rootDir/doc/packages.md".toString()]
        outputFormat = 'gfm'
        outputDirectory = "$rootDir/doc"
    }
}

afterEvaluate {
    publishing {
        repositories {
            maven {
                name = "GithubPackages"
                url = uri("https://maven.pkg.github.com/jtorrestobena/recyclerviewbindingadapter")
                credentials {
                    username = System.getenv('GITHUB_USER') ?: project.properties['GITHUB_USER']
                    password = System.getenv('GITHUB_PERSONAL_ACCESS_TOKEN') ?: project.properties['GITHUB_PERSONAL_ACCESS_TOKEN']
                }
            }
            maven {
                name = 'CustomMavenRepo'
                url = "file://${buildDir}/repo"
            }
        }

        publications {
            // Creates a Maven publication called "release".
            releaseLib(MavenPublication) {
                // Applies the component for the release build variant.
                from components.release

                // You can then customize attributes of the publication as shown below.
                groupId = 'com.bytecoders.recyclerviewbindinglib'
                artifactId = 'recyclerviewbindinglib'
                version = recyclerviewLibraryVersion
                artifact sourceJar
            }
            // Creates a Maven publication called “debug”.
            debugLib(MavenPublication) {
                // Applies the component for the debug build variant.
                from components.debug

                groupId = 'com.bytecoders.recyclerviewbindinglib'
                artifactId = 'debug'
                version = recyclerviewLibraryVersion
            }
        }
    }
}

task sourceJar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
    classifier "sources"
}

dependencies {

    implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlinVersion"
    implementation "androidx.core:core-ktx:$coreKtxVersion"
    implementation "androidx.appcompat:appcompat:$appCompatVersion"
    implementation "com.google.android.material:material:$materialVersion"
    implementation "androidx.databinding:databinding-common:$dataBindingVersion"
    implementation "androidx.databinding:databinding-runtime:$dataBindingVersion"
    testImplementation "junit:junit:$jUnitVersion"
    androidTestImplementation "androidx.test.ext:junit:$androidXjUnitVersion"
    androidTestImplementation "androidx.test.espresso:espresso-core:$espressoVersion"
}

apply plugin: 'maven-publish'