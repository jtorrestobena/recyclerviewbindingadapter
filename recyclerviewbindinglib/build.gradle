plugins {
    id 'com.android.library'
    id 'kotlin-android'
}
apply from: '../sharedproperties.gradle'
android {
    compileSdkVersion 29

    defaultConfig {
        minSdkVersion 21
        targetSdkVersion 29
        versionCode 1
        versionName recyclerview_library_version

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        consumerProguardFiles "consumer-rules.pro"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = '1.8'
    }
}

afterEvaluate {
    publishing {
        repositories {
            maven {
                name = "GithubPackages"
                url = uri("https://maven.pkg.github.com/jtorrestobena/recyclerviewbindingadapter")
                credentials {
                    username = System.getenv('GITHUB_USER') ?: project.properties['GITHUB_USER']
                    password = System.getenv('GITHUB_PERSONAL_ACCESS_TOKEN') ?: project.properties['GITHUB_PERSONAL_ACCESS_TOKEN']
                }
            }
            maven {
                name = 'CustomMavenRepo'
                url = "file://${buildDir}/repo"
            }
        }

        publications {
            // Creates a Maven publication called "release".
            releaseLib(MavenPublication) {
                // Applies the component for the release build variant.
                from components.release

                // You can then customize attributes of the publication as shown below.
                groupId = 'com.bytecoders.recyclerviewbindinglib'
                artifactId = 'recyclerviewbindinglib'
                version = recyclerview_library_version
                artifact sourceJar
            }
            // Creates a Maven publication called “debug”.
            debugLib(MavenPublication) {
                // Applies the component for the debug build variant.
                from components.debug

                groupId = 'com.bytecoders.recyclerviewbindinglib'
                artifactId = 'debug'
                version = recyclerview_library_version
            }
        }
    }
}

task sourceJar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
    classifier "sources"
}

dependencies {

    implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    implementation 'androidx.core:core-ktx:1.3.2'
    implementation 'androidx.appcompat:appcompat:1.2.0'
    implementation 'com.google.android.material:material:1.3.0'
    implementation 'androidx.databinding:databinding-common:4.1.3'
    implementation 'androidx.databinding:databinding-runtime:4.1.3'
    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'androidx.test.ext:junit:1.1.2'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.3.0'
}

apply plugin: 'maven-publish'